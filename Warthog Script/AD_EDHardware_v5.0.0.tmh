//--------------------------------------------------------------------------------------------------------------------------------------------------------------//
// HARDWARE //								Set Device Axis behaviour &  Map MFD Controls																		//
//--------------------------------------------------------------------------------------------------------------------------------------------------------------//
// TOC:/								JOYSTICK AXIS		(7)				RUDDER AXIS			(156)															//
//-----									THROTTLE AXIS		(42)			MFD CONTROLS 		(174)															//
//--------------------------------------------------------------------------------------------------------------------------------------------------------------//
// HARDWARE: JOYSTICK AXIS CURVES //
//--------------------------------------------------------------------------------------------------------------------------------------------------------------//
//					      Up,MVR,DN
	short	JS_CURVE[]	= {0, 1, 2};															// Set via the Autopilot Switch:
	short	JS_ZOOM[]	= {0, 0,-1};															//  [UP is most aggresive, DOWN is least aggressive]
	short	YAW_CURVE[]	= {0, 2, 3};

//--------------------------------------------------------------------------------------------------------------------------------------------------------------//
// HARDWARE: JOYSTICK AXIS MAPPINGS //															// Define All Axis Curves/Trim/Deadzones for Device
//--------------------------------------------------------------------------------------------------------------------------------------------------------------//

		// SET DEFAULT JOYSTICK AXIS //
	int initSetJoystickCurves() {																// Autopilot Select Switch position sets Joystick Curve Profile
		if(DebugOutput) printf("\Function: initSetJoystickCurves()\x0a");
		profile = 1;					// [APAH]												// Three different presets are selectable On-The-Fly
		if(Throttle[APPAT]) profile = 0;
		if(Throttle[APALT]) profile = 2;
		SetSCurve(&Joystick, JOYX, 0, JS_DEADZONE, 0, JS_CURVE[profile], JS_ZOOM[profile]);
		SetSCurve(&Joystick, JOYY, 0, JS_DEADZONE, 0, JS_CURVE[profile], JS_ZOOM[profile]);
		if(UseRuddersT16000 & UseRudderAxisCurves)	SetSCurve(&TFRPRudder, 	 TRUDDER, 		0, TR_DEADZONE, 0, YAW_CURVE[profile], JS_ZOOM[profile]);
		else if(UseRuddersTPR & UseRudderAxisCurves)SetSCurve(&TFRPHARudder, TFRPHA_RUDDER, 0, TR_DEADZONE, 0, YAW_CURVE[profile], JS_ZOOM[profile]);
		if(!fFAOff) {
			if(UseRudderAxisCurves & VerboseOutput) printf("Joystick Profile[%d]: Yaw=%d, Curve=%d, Zoom=%d\x0a", profile, YAW_CURVE[profile], JS_CURVE[profile], JS_ZOOM[profile]);
			else if(VerboseOutput) 					printf("Joystick Profile[%d]: Curve=%d, Zoom=%d\x0a", profile, JS_CURVE[profile], JS_ZOOM[profile]); } }
		


	int initJoystickAxis() { 																	// Set Default Joystick Axis
		if(VerboseOutput) printf("Initialising Joystick Axis... ");
		MapAxis(&Joystick, JOYX, DX_X_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);							// JOY-X Axis
			SetSCurve(&Joystick, JOYX, 0, JS_DEADZONE, 0, 0, 0);
		MapAxis(&Joystick, JOYY, DX_Y_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);							// JOY-Y Axis
			SetSCurve(&Joystick, JOYY, 0, JS_DEADZONE, 0, 0, 0); }


//--------------------------------------------------------------------------------------------------------------------------------------------------------------//
// HARDWARE: THROTTLE AXIS MAPPINGS //															// Define All Axis Curves/Trim/Deadzones for Device
//--------------------------------------------------------------------------------------------------------------------------------------------------------------//

		// THROTTLE UTILITY FUNCTIONS //
	int _trimThrottleAxes(int value) { 															// *TrimDXAxis(axis, +/-1024) trim is additive, SET forces trim value, CURRENT reads axis.pos
		TrimDXAxis(DX_Z_AXIS, SET(value));
		if(SyncLEFTTHRAxis) {	TrimDXAxis(DX_ZROT_AXIS, SET(value)); } }
	
	int _setThrottleSCurve(int lower=0, int center=0, int upper=0, int curve=0, float zoom=0) { // *SetSCurve(&Device, axis, left_deadzone%, center_deadzone%, right_deadzone%, curve(-20...20), scale)
		SetSCurve(&Throttle, THR_RIGHT, lower, center, upper, curve, zoom);						
		if(SyncLEFTTHRAxis) {	SetSCurve(&Throttle, THR_LEFT, lower, center, upper, curve, zoom); } }

	int _setThrottleJCurve(float in, float out) {												// *SetJCurve(&device, axis, physical axis value%, DirectX output value%)
		SetJCurve(&Throttle, THR_RIGHT, in, out);
		if(SyncLEFTTHRAxis) {	SetJCurve(&Throttle, THR_LEFT, in, out); } }

	int _setThrottleCustomCurve(int list) {														// *SetCustomCurve(&device, axis name, LIST(Axis physical position 1, Axis output Value 1, Axis physical position 2, Axis output value 2, …)
		SetCustomCurve(&Throttle, THR_RIGHT, list);
		if(SyncLEFTTHRAxis) {	SetCustomCurve(&Throttle, THR_LEFT, list); } }


																								// FLAP position sets Throttle Curve Profile
		// MAP THROTTLE AXIS PRESETS //															// Three different presets are selectable On-The-Fly
	int _doThrottleFwdOnly() {																	// FWD ONLY : Initialising Throttle Forward Only Preset (SRV etc)
		_trimThrottleAxes(-1024);
		if(CruiseOnThrottle) {	_setThrottleSCurve(0, -95, 0, 0);  }							// Trims Axis 5% when Supercruise On Throttle Active
		else 				 {	_setThrottleSCurve(0, -100, 0, 0); }
		if(VerboseOutput) printf("Throttle Profile: FORWARD ONLY [0 -> 100]\x0a");
		SetThrottle(); }

	int _doThrottleFwdNonLinear() {																// FWD ONLY NL : Initialising Throttle Forward Only Non-Linear Preset (SRV etc)
		_trimThrottleAxes(-1024);																// <Unmapped by Default> (See User Preferences to Set)
		_setThrottleCustomCurve(AxisCurveMod);													// DX-SLIDER Non-Linear progression Curve
		if(VerboseOutput) printf("Throttle Profile: NON-LINEAR FORWARD ONLY [0 ~> 100]\x0a");
		SetThrottle(); }

	int _doThrottleFullScale() {																// FULL SCALE : Initialising Throttle Full Range & Make Throttle Linear (Default)
		_trimThrottleAxes(0);																	// Initialising Throttle Full Scale Preset
		if(CruiseOnThrottle) {	_setThrottleSCurve(0, TFULL_DEADZONE, 5, 0); }					// Trims Axis 5% when Supercruise On Throttle Active
		else 				 {	_setThrottleSCurve(0, TFULL_DEADZONE, 0, 0); }					// ..(Alternative= "_setThrottleJCurve(50, 50);")
		if(VerboseOutput) printf("Throttle Profile: FULL SCALE [-100 -> 100]\x0a");
		SetThrottle(); }

	int _doThrottleFullNonLinear() {															// FULL SCALE NL : Initialising Throttle Full Range & Make Throttle Non-Linear
		_trimThrottleAxes(0);																	// <Unmapped by Default> (See User Preferences to Set)
		_setThrottleCustomCurve(AxisCurveMod);													// DX-SLIDER Non-Linear progression Curve
		if(VerboseOutput) printf("Throttle Profile: NON-LINEAR [-100 ~> 100]\x0a");
		SetThrottle(); }

	int _doThrottleFullScaleCustom() {															// FULL SCALE CUST : Initialising Throttle Full Range & Max Shorter @ 80% Preset (ALTERNATIVE)
		_trimThrottleAxes(0);																	// Slightly Shorter Forward travel distance for my RSeat RS1 Cockpit setup. :)
		_setThrottleJCurve(80, 100);															// <Unmapped by Default> (See User Preferences to Set)
		if(VerboseOutput) printf("Throttle Profile: FULL SCALE CUSTOM [-100 -> 80]\x0a");
		SetThrottle(); }

	int _doThrottleTrimmed() {																	// TRIMMED : Initialising Throttle Trimmed 25% @ Beginning/End Preset (ALTERNATIVE)
		_trimThrottleAxes(0);																	// <Unmapped by Default> (See User Preferences to Set)
		_setThrottleSCurve(-25, 0, -25, 0);
		if(VerboseOutput) printf("Throttle Profile: TRIMMED [-75 -> 75]\x0a");
		SetThrottle(); }

	int _doThrottlePrecision() {																// PRECISION : Initialising Throttle Precision Preset (Docking etc)
		_trimThrottleAxes(0);																	// Allows Throttle Movement ONLY to the middle of blue zone
		_setThrottleSCurve(0, 2, 0, 2, -2);
		if(VerboseOutput) printf("Throttle Profile: PRECISION [-50 -> 50]\x0a");
		SetThrottle(); }



		// SET THROTTLE CURVES //																// FLAP position sets Throttle Curve Profile
	int initSetThrottleCurves() {																// Three different presets are selectable On-The-Fly
		mThrottleFwdOnly		 = EXEC("_doThrottleFwdOnly();");								// Initialising Throttle Forward Only Preset (SRV etc)
		mThrottleFwdNonLinear	 = EXEC("_doThrottleFwdNonLinear();");							// Initialising Throttle Forward Only Non-Linear Preset (SRV etc)
		mThrottleFullScale		 = EXEC("_doThrottleFullScale();");								// Initialising Throttle Full Range & Make Throttle Linear (Default)
		mThrottleFullNonLinear	 = EXEC("_doThrottleFullNonLinear();");							// Initialising Throttle Full Range & Make Throttle Non-Linear
		mThrottleFullScaleCustom = EXEC("_doThrottleFullScaleCustom();");						// Initialising Throttle Full Range & Max Shorter @ 80% Preset (ALTERNATIVE)
		mThrottleTrimmed		 = EXEC("_doThrottleTrimmed();");								// Initialising Throttle Trimmed 25% @ Beginning/End Preset (ALTERNATIVE)
		mThrottlePrecision		 = EXEC("_doThrottlePrecision();"); }							// Initialising Throttle Precision Preset (Docking etc)



		// SET DEFAULT THROTTLE AXIS //
	int initThrottleAxis() { 																	// Set Default Throttle Axis
		if(VerboseOutput) printf("Initialising Throttle Axis..\x0a");
		if(VerboseOutput) printf("Initialising Slew Axis... Setting ");
		MapAxis(&Throttle, THR_RIGHT, DX_Z_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);					// Set Right Throttle
			SetJCurve(&Throttle, THR_RIGHT, 50, 50);
			//if(VerboseOutput) printf("Setting ");
			if(ThrottleOverride == 0) {
				if		(Throttle[FLAPU]) ActKey(KEYON+PULSE+ThrottlePresetUP); 				// Configure Throttle FLAP Switch Position on Script Run
    			else if	(Throttle[FLAPM]) ActKey(KEYON+PULSE+ThrottlePresetMID);
    			else 					  ActKey(KEYON+PULSE+ThrottlePresetDOWN); }
    		if(ThrottleOverride == 1)     ActKey(KEYON+PULSE+ThrottlePresetDEF);				// Configure Throttle Axis Default When FLAP Switch is Customised in User Preferences.
		if(!DisableLEFTTHRAxis) {
			MapAxis(&Throttle, THR_LEFT, DX_ZROT_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);				// Set Left Throttle
			SetSCurve(&Throttle, THR_LEFT, 0, 0, 0, 0, 0); }
		MapAxis(&Throttle, THR_FC, DX_SLIDER_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);					// Set Friction Control Axis Curve (Sensor Range Zoom Scaling)
			if(CustomDXSliderCurve == 0) SetSCurve(&Throttle, THR_FC, 0, 0, 0, 0, 0);			// (DEFAULT)
			if(CustomDXSliderCurve == 1) SetCustomCurve(&Throttle, THR_FC, AxisCurveMod); 		// (OPTIONAL OVERRIDE) (Linear Recommended in ED) *LIST(Axis physical position, Axis output Value)
		if(VerboseOutput & JoystickOverride)  printf("Setting Joystick Profile: Curve=%d, Zoom=%d\x0a", JS_CURVE[JoystickPresetDEF], JS_ZOOM[JoystickPresetDEF]); // NOTE: For print out positioning/order (On Startup), this Joystick Axis Preset output text is in the Throttle Axis Preset function.
		if(VerboseOutput) printf("-----------------------------------------------------------------------------------------------------------------------------------------------\x0a"); }



	int SetThrottle() {																			// Prevents Throttle jumping around on mode changes
		if(DebugOutput) printf("\Function: SetThrottle()\x0a");
		DXAxis(DX_Z_AXIS, -AxisVal(Throttle[THR_RIGHT], &axdata));
		if(!DisableLEFTTHRAxis & SyncLEFTTHRAxis) {
			DXAxis(DX_ZROT_AXIS, -AxisVal(Throttle[THR_LEFT], &axdata)); } }


	int initSlewAxis() {						// NEW!!
		if(SlewAxisPreset == 1) {			// *SetSCurve(&Device, axis, left_deadzone%, center_deadzone%, right_deadzone%, curve(-20...20), scale)
			MapAxis(&Throttle, SCX, DX_XROT_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);						// Set Slew Control X Axis
				SetSCurve(&Throttle, SCX, 0, SLEW_DEADZONE, 0, 2, -1);
			MapAxis(&Throttle, SCY, DX_YROT_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);						// Set Slew Control Y Axis
				SetSCurve(&Throttle, SCY, 0, SLEW_DEADZONE, 0, 2, -1);
			}
		if(SlewAxisPreset == 0) {
			MapAxis(&Throttle, SCX, DX_XROT_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);						// Set Slew Control X Axis
				SetSCurve(&Throttle, SCX, 0, SLEW_DEADZONE, 0, 1, 0);
			MapAxis(&Throttle, SCY, DX_YROT_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);						// Set Slew Control Y Axis
				SetSCurve(&Throttle, SCY, 0, SLEW_DEADZONE, 0, 1, 0); }
		if(SlewAxisPreset == 2) {
			MapAxis(&Throttle, SCX, DX_XROT_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);						// Set Slew Control X Axis
				SetSCurve(&Throttle, SCX, 0, SLEW_DEADZONE, 0, 0, 0);
			MapAxis(&Throttle, SCY, DX_YROT_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);						// Set Slew Control Y Axis
				SetSCurve(&Throttle, SCY, 0, SLEW_DEADZONE, 0, 0, 0); } }
		//else {
		//	if(VerboseOutput) { printf("ERROR: SlewAxisPreset set to invalid value of %d.\x0a", SlewAxisPreset);
		//						printf("* Please change it to SlewPresetSlow = 1, SlewPresetDefault = 0 or SlewPresetFast = 2 in User Prefs *\x0a"); } } }


//--------------------------------------------------------------------------------------------------------------------------------------------------------------//
// HARDWARE: RUDDER AXIS MAPPINGS //															// Define All Axis Curves/Trim/Deadzones for Device
//--------------------------------------------------------------------------------------------------------------------------------------------------------------//

		// SET DEFAULT RUDDER AXES //
	int initTFRPRudder() {																		// Initialise Thrustmaster TFRP (T.Flight T16000) Toe Pedal Axes (When enabled in AD_EDSettings)
		if(DebugOutput) printf("\Function: initTFRPRudder()\x0a");
		SetSCurve(&TFRPRudder, TRUDDER,  0, TR_DEADZONE,  0, 0, 0);								// Rudder Axis
		SetSCurve(&TFRPRudder, TRPRIGHT, 0, TOE_DEADZONE, 0, 0, 0);								// Right Toe Brake
		SetSCurve(&TFRPRudder, TRPLEFT,  0, TOE_DEADZONE, 0, 0, 0); }							// Left  Toe Brake



	int initTFRPHARudder() {																	// Initialise Thrustmaster TFRPHA (T.Flight TPR) Toe Pedal Axes (When enabled in AD_EDSettings)
		if(DebugOutput) printf("\Function: initTFRPHARudder()\x0a");
		SetSCurve(&TFRPHARudder, TFRPHA_RUDDER,	0, TR_DEADZONE,  0, 0, 0);						// Rudder Axis
		SetSCurve(&TFRPHARudder, TFRPHA_RIGHT,	0, TOE_DEADZONE, 0, 0, 0);						// Right Toe Brake
		SetSCurve(&TFRPHARudder, TFRPHA_LEFT,	0, TOE_DEADZONE, 0, 0, 0); }					// Left  Toe Brake


//--------------------------------------------------------------------------------------------------------------------------------------------------------------//
// HARDWARE: MFD CONTROLS //
//--------------------------------------------------------------------------------------------------------------------------------------------------------------//

	int initRightMFD() {																		// Set Right MFD 
		MapKey(&RMFD, OSB01, CHAIN(PULSE+USB[0x2A]));											// DSS EXIT MODE
		MapKey(&RMFD, OSB02, mReportCrimesToggle);												// report crimes
		MapKey(&RMFD, OSB03, mJumpPrevSubSys);													// select powerplant
		MapKey(&RMFD, OSB04, mHangarAutomation);												// Repair, refuel, restock
		MapKey(&RMFD, OSB05, mNAVBeaconToggle);													// NAV Beacon
//		MapKey(&RMFD, OSB06, 0);
//		MapKey(&RMFD, OSB07, 0);
//		MapKey(&RMFD, OSB08, 0);
//		MapKey(&RMFD, OSB09, 0);
//		MapKey(&RMFD, OSB10, 0);
//		MapKey(&RMFD, OSB11, 0);
//		MapKey(&RMFD, OSB12, 0);
//		MapKey(&RMFD, OSB13, 0);
//		MapKey(&RMFD, OSB14, 0);
//		MapKey(&RMFD, OSB15, 0);
		MapKey(&RMFD, OSB16, CHAIN(PULSE+USB[0x56]));											// -100% speed
		MapKey(&RMFD, OSB17, CHAIN(PULSE+USB[0x60]));											// - 50% Speed
		MapKey(&RMFD, OSB18, CHAIN(PULSE+USB[0x1B]));											// 0% speed
		MapKey(&RMFD, OSB19, CHAIN(PULSE+USB[0x5A]));											// 50% speed
		MapKey(&RMFD, OSB20, CHAIN(PULSE+USB[0x57]));											// 100% speed
		MapKey(&RMFD, BRTU,  EXEC("initSendChatString(chat_Basic_LEFT);"));						// "Thank you for your assistance! o7"	
		MapKey(&RMFD, BRTD,  EXEC("initSendChatString(chat_Basic_RIGHT);"));					// "Can I help you with something CMDR?"
//		MapKey(&RMFD, CONU,  0);
//		MapKey(&RMFD, COND,  0);
		MapKey(&RMFD, GAINU, CHAIN(PULSE+L_ALT+USB[0x09]));										// FSS ON
		MapKey(&RMFD, GAIND, CHAIN(PULSE+L_SHIFT+USB[0x09]));									// FSS OFF
//		MapKey(&RMFD, SYMU,  0);
//		MapKey(&RMFD, SYMD,  0);
		if(VerboseOutput)	 printf("Right MFD Configured\x0a"); }


	int initLeftMFD() {																			// Set Left MFD
		MapKey(&LMFD, OSB01, CHAIN(PULSE+L_ALT+USB[0x17]));										// Attack Target
		MapKey(&LMFD, OSB02, CHAIN(PULSE+L_ALT+USB[0x04]));										// Agressive
		MapKey(&LMFD, OSB03, CHAIN(PULSE+L_ALT+USB[0x07]));										// Defensive
		MapKey(&LMFD, OSB04, CHAIN(PULSE+L_ALT+USB[0x16]));										// Hold Psition
		MapKey(&LMFD, OSB05, CHAIN(PULSE+L_ALT+USB[0x05]));										// Dock Fighter
		MapKey(&LMFD, OSB06, CHAIN(PULSE+USB[0x1C]));											// Highest Threat
		MapKey(&LMFD, OSB07, CHAIN(PULSE+USB[0x0B]));											// Next Hostile Ship
		MapKey(&LMFD, OSB08, CHAIN(PULSE+USB[0x25]));											// Wingman 1
		MapKey(&LMFD, OSB09, CHAIN(PULSE+USB[0x26]));											// Wingman 2
		MapKey(&LMFD, OSB10, CHAIN(PULSE+USB[0x27]));											// Wingman 3
		MapKey(&LMFD, OSB11, CHAIN(PULSE+USB[0x10]));											// Next System in Route
//		MapKey(&LMFD, OSB12, 0);
		MapKey(&LMFD, OSB13, L_ALT+USB[0x43]);													// screenshot
		MapKey(&LMFD, OSB14, HideShipGUI);														// Hide Hud
		MapKey(&LMFD, OSB15, CHAIN(PULSE+L_ALT+USB[0x44]));										// video
		MapKey(&LMFD, OSB16, CHAIN(PULSE+USB[0x13]));											// Game Menu
		MapKey(&LMFD, OSB17, CHAIN(PULSE+L_ALT+USB[0x3B]));										// Voice Attack ON/Off
		MapKey(&LMFD, OSB18, TrackIRCenter);													// Center TrackIR
		MapKey(&LMFD, OSB19, CHAIN(PULSE+USB[0x48]));											// Pause TrackIR
		MapKey(&LMFD, OSB20, mRequestDock);														// Autodock
		MapKey(&LMFD, BRTU,  EXEC("initSendChatString(chat_Modifier_DOWN);"));					// "Stay calm, cut your engines and prepare to be scanned. Move and you will be fired upon"
		MapKey(&LMFD, BRTD,  EXEC("initSendChatString(chat_Modifier_UP	);"));					// "You're a disgrace! Die! Time to see the Rebuy!"
		MapKey(&LMFD, CONU,  CHAIN(PULSE+R_CTL));												// Wingman Target
		MapKey(&LMFD, COND,  CHAIN(PULSE+R_SHIFT));												// Wingman Nav Lock
		MapKey(&LMFD, GAINU, EXEC("initSendChatString(chat_Basic_UP);")); 						// o7
		MapKey(&LMFD, GAIND, EXEC("initSendChatString(chat_Basic_DOWN);"));						// "Fly safe CMDR"		
		MapKey(&LMFD, SYMU,  EXEC("initSendChatString(chat_Modifier_LEFT);"));					// "CMDR is coming through the mailslot"
		MapKey(&LMFD, SYMD,  EXEC("initSendChatString(chat_Modifier_RIGHT);"));					// "Easy! Watch where you are going CMDR"	
		if(VerboseOutput)	 printf("Left MFD Configured\x0a"); }


//--------------------------------------------------------------------------------------------------------------------------------------------------------------//
